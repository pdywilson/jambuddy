<!DOCTYPE html>
<html>

<head>
    <meta charset="UTF-8">
    <title>Jam Buddy</title>
    <style type="text/css">
        * {
            margin: 0px;
            padding: 0px;
        }

        #CSConsole {
            background-color: #FAFAFA;
            border-top: 1px solid #333333;
            bottom: 0px;
            height: 200px;
            overflow-y: scroll;
            position: fixed;
            width: 100%;
        }
    </style>

    <script type="text/javascript" src="common/Cindy.js"></script>

    <!-- Load TensorFlow.js -->

    <script src="https://cdn.jsdelivr.net/npm/@tensorflow/tfjs@latest"> </script>
    <!-- <script src="common/tf.min.js"></script> -->
    <!-- <script src="common/tonal.min.js"></script> -->

    



<script id="csdraw" type="text/x-cindyscript">
if(visualization & imageready("image"),
    L = [0,0];
    R = [imagesize("image")_1, 0];
    
    read(p) := imagergb(L, R, "image", p, interpolate->false);
    colorplot(L, R, "image",
            p = (#.x /imagesize("image")_1, #.y / imagesize("image")_2);
            f = p.y/3; //feed
            d = p.x+.1;	//killing
            nbh = .5*read(# + (-1, 1)) +
                read(# + (0,  1)) +
                .5*read(# + (1,  1)) +
                read(# + (-1, 0)) +
                read(# + (1,  0)) +
                .5*read(# + (-1,-1)) +
                read(# + (0, -1)) +
                .5*read(# + (1, -1));
                        
            nbh = nbh/8;
            nc = if(pressed & |p, mouse()| < .02, (0, 0, 0), 
                read(#) + f*nbh - d*(nbh.b, nbh.r, nbh.g);
            );

            forall(1..30,
                if(keyson_#==1,
                    if(mod(#,3)==0, nc.g = nc.g-0.0001;);
                    if(mod(#,3)==1, nc.r = nc.r-0.0001;);
                    if(mod(#,3)==2, nc.b = nc.b-0.0001;);
                    ););
            
            (min(1, max(nc.r, 0)), min(1, max(nc.g, 0)), min(1, max(nc.b, 0))); //clamp
    );
            
    
    drawimage([-3, -1], [3, -1], "image",alpha->.2);
    );



//////
//bars
Ch = 1.7;
C.y=Ch;
x1left = -0.2;
x1right = 0.2;
if(C.x<x1left,C.x=x1left);
if(C.x>x1right,C.x=x1right);
draw((x1left,Ch),(x1right,Ch),color->(0,0,0));
apply(0..2,v,
    x = x1left+0.2*v;
    draw((x,Ch+0.03),(x,Ch-0.03),color->(0,0,0));
);
bars = round((C.x+0.2)*5);
C.x = x1left + bars/5;
custombars = [1,2,4];
bars = custombars_(bars+1);
if(bars==1,
drawtext((x1left-0.1,C.y+0.09),"Trade "+bars+" bar",size->10);,
drawtext((x1left-0.1,C.y+0.09),"Trade "+bars+" bars",size->10);
);

////////////
//instrument

D.y = 1.7;
if(D.x<-1.5,D.x=-1.5);
if(D.x>-0.8,D.x=-0.8);
draw((-1.5,D.y),(-0.8,D.y),color->(0,0,0));
apply(0..7,v,
    x = -1.5+v*0.1;
    draw((x,D.y+0.03),(x,D.y-0.03),color->(0,0,0));
);
instrum = round((D.x+1.5)*10) + 1;
D.x = -1.5+(instrum-1)/10;
drawtext((D.x-0.12,D.y+0.1),instrumentnameslist_instrum,size->10);
instr = instrumentlist_instrum;
if(instrumentsinited == 0 & instr != firstinstr,
    loadinstruments(instrumentlist);
    instrumentsinited = 1;
);
//drawtext((-1.8,D.y),"AI Instrument");

////////
//bpm
/*
E.visible = true;
E.x = -2;
if(E.y<1.3,E.y=1.3);
if(E.y>1.7,E.y=1.7);
draw((E.x,1.3),(E.x,1.7),color->(0,0,0));
apply(0..4,v,
    y = 1.3+v*0.1;
    draw((E.x+0.03,y),(E.x-0.03,y),color->(0,0,0));
);
bpm = round((E.y-1.3)*10)*10 + 100;
E.y = 1.3+(bpm-99)/100;
instr = instrumentlist_instrum;
if(instrumentsinited == 0 & instr != firstinstr,
    loadinstruments(instrumentlist);
    instrumentsinited = 1;
);
*/

////////
//chords
radius = 0.35;
polar(alpha):=(cos(alpha),sin(alpha));

chordCircle(P,midpoint,mode):=(
    drawcircle(midpoint,radius,color->(0,0,0));
    
    if(abs(P-midpoint) != radius, P.xy=(midpoint+(P-midpoint)/abs(P - midpoint)*radius));
    
    Pshifted=(P-midpoint)/radius;
    Pchord = mod(round(arctan2(Pshifted)/30°)+6,12);
    P.xy = midpoint+polar(Pchord*pi/6+pi)*radius;
    P.visible=true;

    if(mode,
        drawtext((midpoint)-(0.05,0.05),chordFifths_(12-((mod(Pchord-4,12)+1)-1)),size->18);,
        drawtext((midpoint)-(0.1,0.05),chordFifths_(12-((mod(Pchord-4,12)+1)-1))+"m",size->18);
    );
    
    chordMapping = [3,8,1,6,11,4,9,2,7,0,5,10];
    ch = chordMapping_(Pchord+1);
    if(mode,
        ch = ch+12;
    );
    ch;
);

Cmidpointh = 1;
Cmidpointx = -1.5;
Cmxf = 0.8;
ch1midpoint = (Cmidpointx+0*Cmxf,Cmidpointh);
ch2midpoint = (Cmidpointx+1*Cmxf,Cmidpointh);
ch3midpoint = (Cmidpointx+2*Cmxf+0.6,Cmidpointh);
ch4midpoint = (Cmidpointx+3*Cmxf+0.6,Cmidpointh);

ch1 = chordCircle(C1,ch1midpoint,ch1mode);
if(bars > 1, ch2 = chordCircle(C2,ch2midpoint,ch2mode);, C2.visible=false;);
if(bars > 2, ch3 = chordCircle(C3,ch3midpoint,ch3mode);
             ch4 = chordCircle(C4,ch4midpoint,ch4mode);, C3.visible=false; C4.visible=false;);


chords = [ch1,ch2,ch3,ch4];


///////////
//sequencer
sx = 0.5;
sxf = 0.2;
if(inited==1,
    drawtext((sx,text),floor(floor((beat-1)/4)+1), size->15);
    drawtext((sx,text-0.1)," bar", size->10);
    drawtext((sx+sxf,text),floor(mod(beat-1,4)+1), size->15);
    drawtext((sx+sxf,text-0.1)," beat", size->10);
    drawtext((sx+2*sxf,text),bpm, size->15);
    drawtext((sx+2*sxf,text-0.1),"   bpm", size->10);

    
);

timestep = bpm/60*(seconds() - timeatstart);
//print(timestep);

////////////
//makesound
if(on & floor(timestep)>timestepfloor,
    timestepfloor = floor(timestep);
    beat = mod(beat, bars*4) + 1;
    measure = ceil(beat / 4);

    if(mod(beat,2)==1,
            playmelody([["ppp"],[getChord(chords_measure),1]],instrument->5););

    if(beat == 1,
        inited = 1;
        if(inputmelody != [],
            makemusic(););
            
        durations = [0]; 
        timestepfirstbeat=timestep;
    );
    
);


///////////////
//UX
forall(1..30,
    if(keyson_#==1,
        currentkeys_# = currentkeys_#+1/24;,
        if(currentkeys_#!=0,
            inputmelody=inputmelody++[[#+59,seconds()-timeatstart,seconds()-timeatstart+currentkeys_#]];
            currentkeys_#=0,
        );
    );
);


ls=alllines();
apply(ls,l,l.selected=false);


if(help,
    drawtext(helpPos+(0,0.1),"Press play to start. ", size->10);
    drawtext(helpPos,"Use Computer Keyboard ", size->10);
    drawtext(helpPos-(0,0.1),"to play the piano.", size->10);
    drawtext(helpPos-(0,0.2),"Keyboard mapping:", size->10);



    if(help, //German.pressed,

    keys=["A","W","S","E","D","F","T","G","Z","H","U","J","K","O","L","P","º","Þ","Ý","Ü",
        "Y","X","C","V","B","N","M","¼","¾","¿";];
        drawtext(helpPos-(-0.1,0.4),"W E    T Z U   O P",size->15);
        drawtext(helpPos-(0,0.6),"A S D F G H J K L Ö Ä",size->15);,

    keys=["A","W","S","E","D","F","T","G","Y","H","U","J","K","O","L","P","º","Þ","Ý","Ü",
        "Z","X","C","V","B","N","M","¼","¾","¿";];//23+10
        drawtext((-1.95,text-0.4),"W E    T Y U   O P",size->15);
        drawtext((-2,text-0.5),"A S D F G H J K L Ö Ä",size->15);
    );
    ,//else
    drawtext(helpPos,"?",color->(.8,.8,.8),size->15);
);

keypos(n) := (
    c = 0;
    if(n==0,c=0);
    if(n==1,c=0);
    if(n==2,c=1);
    if(n==3,c=1);
    if(n==4,c=2);
    if(n==5,c=3);
    if(n==6,c=3);
    if(n==7,c=4);
    if(n==8,c=4);
    if(n==9,c=5);
    if(n==10,c=5);
    if(n==11,c=6);
    //if(n<5,c=floor(n/2),c=ceil(n/2));
    c;
);

blackkeys = [2,4,7,9,11];
drawkeyw(n) := (
    if(!contains(blackkeys,mod(n,12)),
        octave = floor(n/12);
        keynr = keypos(mod(n,12)+1);
        pos = (octave*7+keynr)/3-hoffset-0.035;
        if (keyson_n == 1, c = .5;, c = .3;);
        kh = voffset;
        w = 0.3;
        l = 1;
        fillpoly(((pos-w,kh),(pos,kh),(pos,kh-l),(pos-w,kh-l)),color->[1,1,1]*c);
    );
);

drawkeyb(n) := (
    if(contains(blackkeys, mod(n,12)),
        octave = floor(n/12);
        keynr = keypos(mod(n,12));
        pos = ((octave*7+keynr)+0.35)/3-hoffset-0.035;
        if (keyson_n == 1, c = .4;, c = .1;);
        kh = voffset+0.002;
        w = 0.2;
        l = 0.6;
        fillpoly(((pos-w,kh),(pos,kh),(pos,kh-l),(pos-w,kh-l)),color->[1,1,1]*c);
    );
);

forall(1..numkeys,drawkeyw(#););
forall(1..numkeys,drawkeyb(#););

;

drawimage(speakerPos,if(on,"on","off"),scale->0.15);

</script>

<script id="cskeydown" type="text/x-cindyscript">


forall(keylist,
    if(key() == keys_# & keyson_# == 0,
      play(#);
      keyson_#=1;
      durations = durations ++ timestep - timestepfirstbeat;
    );
);

forall(specialkeyslist,
    if(key() == specialkeys_# & specialkeyson_# == 0,
      playchord(#);
      specialkeyson_#=1;
  );
);

if(key()==" ",on=!on;);
if(key()=="1",visualization=!visualization;);
</script>


<script id="cskeyup" type="text/x-cindyscript">
forall(keylist,
    if(key()==keys_#,
        playdamp(#);
        keyson_#=0;
    );
);

forall(specialkeyslist,
    if(key()==specialkeys_#,
        playdampchord(#);
        specialkeyson_#=0;
    );
);

</script>
<script id="csmousedown" type="text/x-cindyscript">
    if(|mouse().xy,speakerPos|<0.2,on=!on;);
    if(|mouse().xy,helpPos|<0.4,help=!help;);

    if(|mouse().xy,ch1midpoint|<0.2,ch1mode=!ch1mode;);
    if(|mouse().xy,ch2midpoint|<0.2,ch2mode=!ch2mode;);
    if(|mouse().xy,ch3midpoint|<0.2,ch3mode=!ch3mode;);
    if(|mouse().xy,ch4midpoint|<0.2,ch4mode=!ch4mode;);

    isOnBlackKey(x) := (
        keyarea = floor((x+2)*3 - 0.3) + 1;
        rightedge = (keyarea+0.3)/3 - 2;
        leftedge = rightedge - 0.2;
        if(x<rightedge & x > leftedge,c = true;,c = false;);
        if(keyarea == 0 % keyarea == 3 % keyarea == 7 % keyarea == 10 % keyarea == 12, c=false;);
        c;
    );

    if(mouse().y<0 & mouse().y>-1 & mouse().x<2 & mouse().x>-2,
        if(mouse().y>-0.6 & isOnBlackKey(mouse().x),
            keynr = getChromaBlack(floor((mouse().x + 2)*3 + 0.3));
        ,//else
            //for x coord get key nr
            keynr = getChromaWhite(floor((mouse().x + 2)*3) + 1);
        );
        play(keynr);
        keyson_keynr=1;
        durations = durations ++ timestep - timestepfirstbeat;
            
    );
;
//print(mouse());
</script>
<script id="csmouseup" type="text/x-cindyscript">
    forall(keylist,
        if(keyson_#==1,
            playdamp(#);
            keyson_#=0;
        );
    );
</script>


<script id="csinit" type="text/x-cindyscript">
    use("CindyGL");
    pressed = false;
    visualization = false;        


bars = 2;
inited=0;
on=false;
speakerPos = (0,1);

//helptext
help = false;
text = 1.77;
helpPos = (1.5,1.75);

//DrawKeyboard
hoffset=2;
voffset=0;
numkeys=20;

//Sound
instr = 10;

//harmony
durations = [];
ch1 = 0;
ch2 = 0;
ch3 = 0;
ch4 = 0;
chords = [ch1,ch2,ch3,ch4];
chordnames=["C-","C#-","D-","D#-","E-","F-","F#-","G-","G#-","A-","A#-","B-","C","C#","D","D#","E","F","F#","G","G#","A","A#","B","C"];

chordFifths=["F#","C#","G#","D#","A#","F","C","G","D","A","E","B","F#"];
chordFifthsMinor=["D#m","A#m","F#m","Cm","Gm","Dm","Am","Em","Bm","F#m","C#m","G#m","D#m"];

ch1mode = false;
ch2mode = false;
ch3mode = true;
ch4mode = false;

major = [48,52,55];
minor = [48,51,55];




getChord(nr):=(
    if(nr<12,
        //minor
        tonic=mod(nr,12);
        third=mod(nr+3,12);
        fifth=mod(nr+7,12);
    ,// else major
        tonic=mod(nr,12);
        third=mod(nr+4,12);
        fifth=mod(nr+7,12);
    );

    ch=[48+tonic,48+third,48+fifth];
);

getChromaWhite(n):=(
    if(n==1,c=1);
    if(n==2,c=3);
    if(n==3,c=5);
    if(n==4,c=6);
    if(n==5,c=8);
    if(n==6,c=10);
    if(n==7,c=12);
    if(n==8,c=13);
    if(n==9,c=15);
    if(n==10,c=17);
    if(n==11,c=18);
    if(n==12,c=20);
    c;
);
getChromaBlack(n):=(
    if(n==1,c=2);
    if(n==2,c=4);
    if(n==4,c=7);
    if(n==5,c=9);
    if(n==6,c=11);
    if(n==8,c=14);
    if(n==9,c=16);
    if(n==11,c=19);
    c;
);




bpm = 120;
beat = 0;
timeatstart = floor(seconds());
timestep = bpm/60*(seconds() - timeatstart);
timestepfirstbeat=timestep;
timestepfloor = floor(timestep);


makemusic() := (
    secs = seconds();
    durationsrel = [];
    repeat(length(durations)-1,
        durationsrel = durationsrel ++ (durations_(#+1) - durations_#);
    );
    durationsrel = durationsrel ++ durations_1+4*bars - durations_(-1);
    print("durations "+durations);
    print("durationsrel "+durationsrel);
    continueSequence(
        inputmelody,
        durationsrel,
        chords,
        bars,
        melody = m;
        delay = seconds()-secs+0.5;
        print("delay"+delay);
        forall(1..length(melody),v,
            if(delay > 0, 
                if(delay < melody_v_2,
                    melody_v_2 = melody_v_2 - delay;
                    delay = 0;,
                    //else delay > melody_v_2
                    delay = delay - melody_v_2;
                    melody_v_1 = "p";
                    melody_v_2 = 0;
                );
            );
        );
        
        print(melody);
        playmelody(melody,instrument->instr,speed->bpm);
    );
    inputmelody=[];
);

//init NN
inputmelody=[["p",1,2],[60,2,3]];
durations = [0,1];
makemusic();

//init midi.js
playtone(60);



//instruments
instrumentlist = [5,1,45,74,80,81,99,128];
instrumentnameslist = ["e-piano","piano","strings","flute","ocarina","squarewave","crystal","gunshots"];
instrumentsinited = 0;
firstinstr = instrumentlist_1;


//27 jazz guitar
//45 strings
//74 flute
//80 ocarina
//81 square wave!
//99 spacey
//128 gun shots
//33 acoustic bass ganz ok
//66 alto sax naja

currentkeys=(0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0);


notes=(0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0);
keys=["A","W","S","E","D","F","T","G","Y","H","U","J","K","O","L","P","º","Þ","Ý","Ü"];
specialkeys=["Z","X","C","V","B","N","M","¼","¾","¿";];
keyson = (0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0);
specialkeyson = (0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0);
harms=[1];

num = 20;
keylist = 1..20;
specialkeyslist = 1..10; //zxcvbnm,./

f(n) := 2^((n-49)/12)*440;
grundton = 39; //52=C but i starts at 1
repeat(num,i,
    notes_i=f(i+grundton)
);
//grundtoene unten links
notes_(num+1)=f(grundton-11);
notes_(num+2)=f(grundton-9);
notes_(num+3)=f(grundton-7);
notes_(num+4)=f(grundton-6);
notes_(num+5)=f(grundton-4);
notes_(num+6)=f(grundton-2);
notes_(num+7)=f(grundton);
notes_(num+8)=f(grundton+1);
//special:
notes_(num+9)=f(grundton+21);
notes_(num+10)=f(grundton+22);


am=0.4;
play(i):=playsin(notes_i,amp->100*am/notes_i, damp->1, duration->4, line->i);
playdamp(i):=playsin(notes_i,amp->100*am/notes_i, damp->4, duration->4, harmonics->harms, line->i, restart->false);
playchord(i):=(
    playsin(notes_i,amp->100*am/notes_i, damp->1, duration->4, line->i+"a");
    playsin(notes_i*2^(7/12),amp->100*am/notes_i, damp->1, duration->4, line->i+"b");
    playsin(notes_i*2^(10/12),amp->100*am/notes_i, damp->1, duration->4, line->i+"c");
);
playdampchord(i):=(
    playsin(notes_i,amp->100*am/notes_i, damp->4, duration->4, line->i+"a", restart->false);
    playsin(notes_i*2^(7/12),amp->100*am/notes_i, damp->4, duration->4, line->i+"b", restart->false);
    playsin(notes_i*2^(10/12),amp->100*am/notes_i, damp->4, duration->4, line->i+"c", restart->false);
);




guess(a):=(
    p=guess(a,10);
    p_1+"/"+p_2;
);

guess(a,n):=(
   regional(pp,fl,fr);
   fl=floor(a+0.000001);

   fr=a-fl;
   if(n==0,(fl,1),
     if(|a-round(a)|<0.0001,(fl,1),
         pp=guess(1/fr,n-1);
         [fl*pp_1+pp_2,pp_1];

     );
   );

);

;
</script>

    <script type="text/javascript">
        var cdy = CindyJS({
            scripts: "cs*",
            defaultAppearance: {
                dimDependent: 0.7,
                fontFamily: "sans-serif",
                lineSize: 1,
                pointSize: 5.0,
                textsize: 12.0
            },
            angleUnit: "°",
            geometry: [
                //{name: "German", type: "ToggleButton", pos: [20,20], color: [0.0, 0.0, 0.0], fillcolor: [1.0, 1.0, 1.0], fillalpha: 1.0, alpha: 0.8999999761581421, text: "German Keyboard!"},
                //{name: "A", type: "Free", pos: [0, 1.3], color: [1,.5,.5], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                //{name: "B", type: "Free", pos: [-0.7, 1.5], visible: false, color: [.5,1,.5], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                //{name: "A2", type: "Free", pos: [0, 1.1], visible: false, color: [1,.5,.5], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                //{name: "B2", type: "Free", pos: [-0.7, 0.9], visible: false, color: [.5,1,.5], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                {name: "C", type: "Free", pos: [0.4, 1.8], color: [.5,.5,1], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                {name: "D", type: "Free", pos: [-2, 0.5], color: [.5,.5,1], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                {name: "E", type: "Free", pos: [2, 1.5], visible: false, color: [.5,.5,1], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                {name: "C1", type: "Free", pos: [-1,1.5], color: [.5,1,.5], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                {name: "C2", type: "Free", pos: [0,0.5], visible: false, color: [.5,1,.5], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                {name: "C3", type: "Free", pos: [0.4,1.5], visible: false, color: [.5,1,.5], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
                {name: "C4", type: "Free", pos: [2,.8], visible: false, color: [.5,1,.5], alpha: 0.8999999761581421, textsize: 0.0, size: 6.0, narrow: 40},
            ],
            animation: {autoplay: true},
            autoplay: true,
            keylistener: true,
            use: ["midi", "rnn3"],
            images: {
                image: "common/image.png",
                on: "common/Pause.png",
                off: "common/Play.png"
            },
            ports: [{
                fill: "window",
                id: "CSCanvas",
                virtualwidth: 600,
                virtualheight: 300,
                transform: [{
                    visibleRect: [-2.2, 2, 2.2, -1]
                }],
                background: "rgb(50,50,50)"
            }],
            cinderella: {
                build: 1847,
                version: [2, 9, 1847]
            }
        });
    </script>
</head>

<body>
    <div id="CSCanvas"></div>
<!-- Fullscreen on rotate-->
<script>
        /* Get the element you want displayed in fullscreen mode (a video in this example): */
        var inFullscreen = false;
        
        /* When the openFullscreen() function is executed, open the video in fullscreen.
        Note that we must include prefixes for different browsers, as they don't support the requestFullscreen method yet */
        function openFullscreen() {
            var elem = document.getElementById("CSCanvas"); 
          if (elem.requestFullscreen) {
            elem.requestFullscreen();
          } else if (elem.mozRequestFullScreen) { /* Firefox */
            elem.mozRequestFullScreen();
          } else if (elem.webkitRequestFullscreen) { /* Chrome, Safari and Opera */
            elem.webkitRequestFullscreen();
          } else if (elem.msRequestFullscreen) { /* IE/Edge */
            elem.msRequestFullscreen();
          }
        }

        function closeFullscreen() {
            if (document.exitFullscreen) {
                document.exitFullscreen();
            } else if (document.mozCancelFullScreen) { /* Firefox */
                document.mozCancelFullScreen();
            } else if (document.webkitExitFullscreen) { /* Chrome, Safari and Opera */
                document.webkitExitFullscreen();
            } else if (document.msExitFullscreen) { /* IE/Edge */
                document.msExitFullscreen();
            }
        }

        // Detect whether device supports orientationchange event, otherwise fall back to
        // the resize event.
        // var supportsOrientationChange = "onorientationchange" in window,
        //     orientationEvent = supportsOrientationChange ? "orientationchange" : "resize";

        window.addEventListener("orientationchange", function() {
            if(!inFullscreen){
                console.log("Going fullscreen mode", orientationEvent);
                openFullscreen();
                inFullscreen = true;
            } else{
                // closeFullscreen();
                // inFullscreen = false;
            }
        }, false);
    </script>

</body>

</html>
